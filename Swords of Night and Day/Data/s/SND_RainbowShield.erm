ZVSE

** SND_RainbowShield, JHV, June 2011
*  for "The Swords of Night and Day"
** from Block/Defense-Reduction Skill Glitch Patch, JHV 2010.8.25
**(remove Block skill and replace with simulated block @ same % to prevent glitch)

* uses v1-v7 temporarily, z476-z477, flag 205

!#IF:V205/0; RB Shield not equipped

!#VRz476:S^Rainbow Shield of Blocking^;
!#VRz477:S^{Rainbow Shield of Blocking}
This shield gives (Experience/2)% chance of blocking (Exp.)% damage, up to Exp. 80, and gives +1 Luck.^;
!#UN:A47/9/476;
!#UN:A47/10/477;

!?AE1&v998=47; equip RB Shield
!!IF:V205/1;
!?AE0&v998=47; unequip Shield
!!IF:V205/0;

!?MF1&1000/205;
!!MF:N?v1; v1=stack
!!BMv1:N?v2 T?v3; current number of creatures, type
!!FU&v3<>6/v3<>7/v3<>174/v3<>34/v3<>35/v3<>136|v2<1:E;
!!VRv4:Sv1476; exp
!!VRv4&v1476>80:S80; max
!!VRv5:Sv4:2; exp/2 - chance of block
!!FU&v5<1:E;
!!MF:F?v6; damage
!!VRv7:S100-v4; %damage after block
!!VRv6:*v7+49:100; damage block
!!VRy-1:S1 R99;
!!FU&y-1>v5:E;
!!MF:Fv6; block some damage
!!BMv1:V84;
