ZVSE

** SND_Megathon script, JHV, May 2011
** For "The Swords of Night and Day" map by Jim Vogan

* uses v1429 from SND_Garden, v1432 from SND_Compass, v7092-v7094, v7103, flags 23, 189

!#IF:V23/1; half multiple attacks by Megy

*-*-* multiple attacks per round
!?BA0&1000;
!!VRv7092:C-1/v7103/0;
!!IF:V189/0;
*-*-**-*-**-*-**-*-**-*-**-*-**-*-**-*-**-*-**-*-**-*-**-*-**-*-**-*-*
***
*** Set F997 to 0!  (Gets set by wandering monster trigger)
*** Without this, right-clicking on the Defend icon will not allow you
*** to switch between ranged and melee attacks, but --
*** NOTE: this will screw up a real network battle!
!!IF:V997/0;

!?BG0&189; (used /-186 to disable if V&J have Swords--chgd back)
!!BG:N?v1; stack #
!!FU&v1=v7092:E;
!!IF:V1/1;
!!IF&23:V1/0;
!!IF:V23/0;
!!IF&1:V23/1;
* restore flags for Megathon (must restore AFTER BG1 of Megathon's turn)
!!BMv7092&23/v7092>0:N?v1;
!!BMv7092&23/v7092>0/v1>0:Fv7093;
!!IF&23:V189/0;

!?BG0&1000; save flags for Megathon
!!BG:N?v1; stack #
!!BMv1:F?v2 T?v7094; type
!!FU&v7094<>97:E;
!!VRv7092:Cv1/v2;
!!IF:V189/1;

* Result: one Megathon troop gets another turn after each enemy move IF
*         there are two or more troops, but if there is only one troop
*         the program seems to know that the attacking side turns are over
*         after one move, and all the enemy troops move before the Megathon
*         can move again (in the next round).
*         (So the hero character for The Swords of Night and Day must have
*          a side-kick.)

!?HE75; attack on Nimbus/Devoron (happens twice if N/D wins battle)
!!PO142/142/0:N?v1; use as flag to prevent 2x
!!FU&v1>0:E;
!!PO142/142/0:N1;
!!VRz10:S^In my gut is the Dark Abyss!
I hunger always!
Feed me!^;
!!VRz12:S^Megathon the Beast^;
!!VRz13:S^..\Data\Megathon.bmp^;
!!IF:D34/z10/0/z12/z13 ;
!!IF:F34/0/0/0/0/0;
!!IF:E99/34;
!!IF:Q1/21/v201/1^My name is Vames Morrolan.
You killed my father.
Prepare to die.^;
!!VRv1429:S7;
Restore Skeleton stats
!!MA:E57/3; [Damage High]
!!MA:M57/1; [Damage Low]
!!MA:A57/6 D57/6 P57/6 S57/5; Attack, Defense, HP, Speed
!!if&v1432>0:;
!!VRy-1:Sv2396;
!!VRy-1:+3;
!!UN:C8654048/4/?y-2; read memory address of day of the week to y-2
!!VRy-2:+4; 2 bytes further is week of the month, another 2 bytes further is month number
!!UN:Cy-2/2/y-1;
!!IF:Q1/8/85/1^The payment for your Compass is due!
(3 months added to time for the purpose of the final score... if you win.)^;
!!en:;
