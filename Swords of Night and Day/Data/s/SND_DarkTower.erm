ZVSE

** "SND_DarkTower" by Jim Vogan, Jan. 2013, for "The Swords of Night and Day"

** uses z327, v1420, v1433-v1437, flags 170-173, PO:V0-V3

!#VRz327:S^Dark Tower^;
!#OB86/53/0:SHz327;
!#OB77/66/0:SHz327;
!#OB36/75/0:SHz327;
!#OB97/59/0:SHz327;
!#VRv1420:S0; not fighting Wraiths inside a Dark Tower right now
!#VRv1433:S999330; Some prime number around 1 million, minus 1. Random seed will be from range [0, this number]. Quite many seeds, but not close to overflows.
!#VRv1434:S647; Another, small prime
!#VRv1435:S137; And another one, somewhat close to the number of unlocked artifacts, minimum artifact id that may be picked is 7 (Centaur Axe), maximum is 143 (Monster's Power)
!#VRv1436:S0 Tv1433; Random number used to generate Treasures
!#VRv1433:+1; Back to being prime for modulo operations later (Prime means definitely no small cycles)

!?OB63/47&1000; Dark Tower Level 1
!!IF:Q1^A warning notice is posted: Beware! Evil creatures may have come north on the Night of the Dark Moon and be hiding inside! Enter at your own risk! Will you enter?^;
!!FU&-1:E;
!!PO998:V0/?v1;
!!VRv2:Sc0-2:28; game month-1, chgs on Tuesday
!!IF&v2<v1:Q1^You enter through a tunnel into the dungeon level. This level is empty this month. Will you climb the stairs to the next level?^;
!!IF&v2<v1/1:V170/1;
!!FU&v2<v1:E;
!!VRv1:Sv2+1; next month(-1)
!!PO998:V0/v1;
!!VRy1:Sv2+1*24; # of monsters
** table of monster #'s for dungeon
!!VRv1:C42/70/84/98/143;  imp/trog/goblin/gnoll/rogue
!!VRv6:S1 R4; random # 1-5
!!VRy3:Svv6; y3 = monster for HL 1-5
!!IF:M^Your curiosity stirred by rumors of hidden wealth, you enter the Tower through a tunnel into the dungeon level, only to be surprised by a horde of monsters!^;
!!HE-1:T50/50/1/y3/y1;  subterreanan underground
!!HE27:O?v1; get owner (-1 if hero lost battle)
!!FU&v1<>3:E;
!!IF:N6/200/0/5/2/5;
!!IF:N^You find gold, wood, and ore!^;
**[Add gold to player's total]
!!OW:R-1/6/d200;
**[Add wood to player's total]
!!OW:R-1/0/d5;
**[Add ore to player's total]
!!OW:R-1/2/d5;
!!FU86:P1; give exp.
!!UN:R1;
!!IF:Q1^Will you climb the stairs to the next level?^;
!!IF&1:V170/1;

!?OB63/47&170; Dark Tower Level 2
!!IF:V170/0;
!!PO998:V1/?v1;
!!VRv2:Sc0-2:28; game month-1, chgs on Tuesday
!!IF&v2<v1:Q1^You climb up to the first floor. This level is empty this month. Will you climb the stairs to the next level?^;
!!IF&v2<v1/1:V171/1;
!!FU&v2<v1:E;
!!VRv1:Sv2+1; next month(-1)
!!PO998:V1/v1;
!!VRy1:Sv2+1*16; # of monsters
!!VRz-1:S^WRTHWNCE.WAV^;
!!SN:Pz-1;
!!IF:Q1/21/61/1^The room is full of ...^;
!!VRv1420:S1; information for Wraiths' max hp/Artifact draining script in Wraiths.erm - fighting Wariths inside a Dark Tower right now
!!HE-1:T137/124/0/61/y1;  lava/evil fog
!!VRv1420:S0; fight over
!!HE27:O?v1; get owner (-1 if hero lost battle)
!!FU&v1<>3:E;
!!IF:N6/400/1/5/3/5;
!!IF:N^You find gold, mercury, and sulfur!^;
**[Add gold to player's total]
!!OW:R-1/6/d400;
**[Add mercury to player's total]
!!OW:R-1/1/d5;
**[Add sulfur to player's total]
!!OW:R-1/3/d5;
!!FU86:P2; give exp.
!!UN:R1;
!!IF:Q1^Will you climb the stairs to the next level?^;
!!IF&1:V171/1;

!?OB63/47&171; Dark Tower Level 3
!!IF:V171/0;
!!PO998:V2/?v1;
!!VRv2:Sc0-2:28; game month-1, chgs on Tuesday
!!IF&v2<v1:Q1^You climb up to the second floor. This level is empty this month. Will you climb the stairs to the next level?^;
!!IF&v2<v1/1:V172/1;
!!FU&v2<v1:E;
!!VRv1:Sv2+1; next month(-1)
!!PO998:V2/v1;
!!VRy1:Sv2+1*8; # of monsters
!!VRz-1:S^VAMPWNCE.WAV^;
!!SN:Pz-1;
!!IF:Q1/21/63/1^The room is full of ...^;
!!HE-1:T137/124/0/63/y1;  lava/evil fog
!!HE27:O?v1; get owner (-1 if hero lost battle)
!!FU&v1<>3:E;
!!IF:N6/600/4/5/5/5;
!!IF:N^You find gold, crystal, and gems!^;
**[Add gold to player's total]
!!OW:R-1/6/d600;
**[Add crystal to player's total]
!!OW:R-1/4/d5;
**[Add gems to player's total]
!!OW:R-1/5/d5;
!!FU86:P3; give exp.
!!UN:R1;
!!IF:Q1^Will you climb the stairs to the next level?^;
!!IF&1:V172/1;

!?OB63/47&172; Dark Tower Level 4
!!IF:V172/0;
!!PO998:V3/?v1;
!!VRv2:Sc0-2:28; game month-1, chgs on Tuesday
!!IF&v2<v1:M^You climb up to the top floor. This level is empty this month.^;
!!FU&v2<v1:E;
!!VRv1:Sv2+1; next month(-1)
!!PO998:V3/v1;
!!VRy1:Sv2+1*2-1; # of monsters
!!VRz-1:S^VAMPATTK.WAV^;
!!SN:Pz-1;
!!IF:Q1/21/196/1^The room is full of ...^;
!!HE-1:T137/124/0/196/y1;  lava/evil fog
!!HE27:O?v1; get owner (-1 if hero lost battle)
!!FU&v1<>3:E;
!!VRy2:S1 R1; 1-2
!!VRy3:S1 R1; 1-2
!!VRy5:S2*y2*y3; 2,4, or 8 (minor, treasure, major)
!!UN:J6/y5/?y6; random artifact in case fixed random doesn't return anything
!!VRv1437:S0;
!!DO654/1/500/1:P; Search for next Artifact, try at most 500 times
!!VRy6&v1437>0:Sv1437; Select the fixed-random Artifact, if found, otherwise stick with true-random
!!IF:Q1/6/1000/8/y6/1^You find gold and an artifact!^;
!!OW:R-1/6/d1000;
!!HE-1:A4/y6;
!!FU86:P5; give exp.
!!UN:R1;

!?FU654; select the next pseudo-random minor, treasure or major artifact enabled in map settings.
!!VRy1:Sv1436%v1435+7; Candidate Artifact number
!!VRv1436:*v1434%v1433; Set next seed for Artifact selection
!!UN:Ay1/?y2; Check if the Artifact is enabled in map settings
!!UN:Ay1/3/?y3; Check the Artifact's class
!!VRv1437&y2=0/y3>1/y3<16:Sy1; If Artifact enabled and correct class, return result
!!VRx16&y2=0/y3>1/y3<16:S647; and end loop
