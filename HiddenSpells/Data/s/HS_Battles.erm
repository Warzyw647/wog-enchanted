ZVSE

; HS_Battles, JHV, Sept. 27, 2012, based on:
; SND_BAttles, May 2011, updated Aug 27 for Chain Lightning messages (woG 3.58f)
; update Mar 15, 2017--added HL to call FU86 after Tree of Knowledge visit, requires TUT

; uses v1-v11 (temp.), v1297 (from HS), v1298, v1477, FU85-FU87, FU94, FU138, FU301, flags 200-205
!#VRv1477:S0; V-J experience points
!#IF:V200/0 V201/1 V202/1 V203/1 V204/1;

!#EA0:F119/65/?v1; v1=line for ability 119/65, or empty line if none, -1 if no free lines
!#EA0&v1>-1:Bv1/1/119/65/1/1/1/1/1/1/1/1/1/1/1; Immunity to Hostile Air
!#EA1:F119/65/?v1; v1=line for ability 119/65, or empty line if none, -1 if no free lines
!#EA1&v1>-1:Bv1/1/119/65/1/1/1/1/1/1/1/1/1/1/1; Immunity to Hostile Air
*#EA35:B7/1/97/19/100/100/100/100/100/100/100/100/100/100/100; Chain Lightning after attack--works after hit only
!#EA34:B8/1/119/65/1/1/1/1/1/1/1/1/1/1/1; Immunity to Hostile Air
!#EA34:F76/-1/?v1; v1=line for ability 76/-1, or empty line if none, -1 if no free lines
!#EA34&v1>-1:Bv1/1/76/19/25/25/25/25/25/35/35/35/35/35/45;  25/35/45% chance to deflect 20% of damage
!#EA35:B8/1/119/65/1/1/1/1/1/1/1/1/1/1/1; Immunity to Hostile Air
!#EA35:F76/-1/?v1; v1=line for ability 76/-1, or empty line if none, -1 if no free lines
!#EA35&v1>-1:Bv1/1/76/29/40/40/40/40/40/50/50/50/50/50/60;  40/50/60% chance to deflect 30% of damage
!#EA136:B8/1/119/65/1/1/1/1/1/1/1/1/1/1/1; Immunity to Hostile Air
!#EA6:B8/1/119/65/1/1/1/1/1/1/1/1/1/1/1; Immunity to Hostile Air
!#EA6:F76/-1/?v1; v1=line for ability 76/-1, or empty line if none, -1 if no free lines
!#EA6&v1>-1:Bv1/1/76/19/25/25/25/25/25/35/35/35/35/35/45;  25/35/45% chance to deflect 20% of damage
!#EA6:F104/-1/?v1; v1=line for ability 104/-1, or empty line if none, -1 if no free lines
!#EA6&v1>-1:Bv1/1/104/83/0/1/2/3/4/5/6/7/9/9/10; Black Dragon Hatred;  Black Dragon Hatred
!#EA7:B7/1/104/83/d/d/d/d/d/d/d/d/d/d/d; Black Dragon Hatred
!#EA7:B8/1/104/155/d/d/d/d/d/d/d/d/d/d/d; Darkness Dragon Hatred
!#EA7:B9/1/119/65/1/1/1/1/1/1/1/1/1/1/1; Immunity to Hostile Air
!#EA7:F76/-1/?v1; v1=line for ability 76/-1, or empty line if none, -1 if no free lines
!#EA7&v1>-1:Bv1/1/76/29/40/40/40/40/40/50/50/50/50/50/60;  40/50/60% chance to deflect 30% of damage
!#EA174:O7;
!#EA34:F112/-1/?v1; v1=line for ability 112/-1, or empty line if none, -1 if no free lines
!#EA34&v1>-1:Bv1/1/112/15/100/100/100/100/100/100/100/100/100/100/100; Magic Arrow before attack
!#EA35:F112/-1/?v1; v1=line for ability 112/-1, or empty line if none, -1 if no free lines
!#EA35&v1>-1:Bv1/1/112/17/100/100/100/100/100/100/100/100/100/100/100; Lightning before attack
!#EA174:F76/-1/?v1; v1=line for ability 76/-1, or empty line if none, -1 if no free lines
!#EA174&v1>-1:Bv1/1/76/66/50/70/70/70/70/85/85/85/85/85/100;  70/85/100% chance to deflect 67% of damage
!#EA174:F115/-1/?v1; v1=line for ability 115/-1, or empty line if none, -1 if no free lines
*#EA174&v1>-1:Bv1/1/115/53/0/0/1/1/1/2/2/2/3/3/3; Enchant w/Haste (3=expert, 4=mass, 5=mass+R, 6=mass+RE)
!#EA174&v1>-1:Bv1/1/115/53/0/0/1/1/1/1/1/1/1/1/1; Enchant w/Haste (3=expert, 4=mass, 5=mass+R, 6=mass+RE)
!#EA174:B12/1/103/37/20/20/20/20/20/30/30/30/30/30/40;  resist 20/30/40% of spell damage
!#EA136:B6/1/112/19/100/100/100/100/100/100/100/100/100/100/100; Chain Lightning before attack
!#EA136:B7/1/119/53/1/1/1/1/1/1/1/1/1/1/1; Immunity to all hostile spells
!#EA136:B8/1/76/66/50/70/70/70/70/85/85/85/85/85/100;  70/85/100% chance to deflect 67% of damage
!#EA136:B9/1/115/53/0/0/1/1/1/2/2/2/3/3/3; Enchant w/Haste (3=expert, 4=mass, 5=mass+R, 6=mass+RE)
!#EA136:B10/1/105/61/1/1/1/1/1/1/1/1/1/1/1; no range penalty
!#EA136:B11/1/70/61/1/1/1/1/1/1/1/1/1/1/1; Fearsome
!#MA:P174/50 P136/50;

!?MR1; spell damage/resistance trigger, final
!!MR:M?v5 S?v6; monster type, spell #
!!FU&v6<>19:E; exit if not Chain L
!!BG:A?v7 N?v8; action type, casting stack
!!VRv9:S-1;
!!BMv8&v8>-1/v8<42:T?v9;
!!FU|v7<6/v7>7/v9<>136:E; exit if not Jalery
!!MR:F600; [D600--no effect on battle message, try MR0--D600 has no effect there too]
; eliminate message "The %s does %d damage." ("The Chain Lightning does 220")
!!UN&v1297>0:Cv1297/2/8192; replace "Th" = 54 68 = 26708 with " " = 20 00 = 8192
!!IF:V205/1;

!?BG1&205;
!!UN&v1297>0:Cv1297/2/26708; restore "Th"
!!VRz10:S^The High Enchanter spell does 600 damage at each strike.^;
!!BU:Mz10;
!!IF:V205/0;

; 1st battle: prayers

!?BG0&1000/201;
!!BU:T?v1;
!!FU&v1=1:E; No prayers during Tactics phase to allow for HD mod's quick stack selection in Tactics by clicking that stack
!!BG:H?v1;
!!FU&v1<>27:E; Only pray to Leora, not Calladan in the Arena
!!BG:N?v1; stack
!!BMv1:T?v2;
!!FU&v2<>136:E;
!!IF:Q1/21/136/1^Leora, thank you for your gifts.  In this desperate struggle, we ask that you stint not your power, but use it on each of us, in turn.^;
!!IF:V201/0;

!?BG0&1000/202;
!!BU:T?v1;
!!FU&v1=1:E; No prayers during Tactics phase
!!BG:H?v1;
!!FU&v1<>27:E; Only pray to Leora, not Calladan in the Arena
!!BG:N?v1; stack
!!BMv1:T?v2;
!!FU&v2<>174:E;
!!IF:Q1/21/174/1^Leora, if it be thy will, shield us to reduce the power of the foe's strikes.^;
!!IF:V202/0;

!?BG0&1000/203;
!!BU:T?v1;
!!FU&v1=1:E; No prayers during Tactics phase
!!BG:H?v1;
!!FU&v1<>27:E; Only pray to Leora, not Calladan in the Arena
!!BG:N?v1; stack
!!BMv1:T?v2;
!!FU&v2<>34:E;
!!IF:Q1/21/34/1^Leora, we ask for your Blessing, that if we die, it will be in glory.^;
!!IF:V203/0;

!?BG0&1000/204;
!!BU:T?v1;
!!FU&v1=1:E; No prayers during Tactics phase
!!BG:H?v1;
!!FU&v1<>27:E; Only pray to Leora, not Calladan in the Arena
!!BG:N?v1; stack
!!BMv1:T?v2;
!!FU&v2<>6:E;
!!IF:Q1/21/6/1^Leora, if you can, we ask you to heal our wounds that we may fight to the last.^;
!!IF:V204/0;

; casting for each stack per round

!?BA0&1000;
!!IF:V200/0;
!!VRv1298:S-1;
!!VRy-1:S0 T20;
!!DO301/0/y-1/1:P; randomize battles

!?FU301; battle randomizer
!!VRy1:R100;

!?BR&1000;
!!BA:O?y1/?y2;
!!DO85/0/20/1&y1<>3:P; give enemy creatures exp. based on Leora's, refresh at start of each turn in case there are summons with wrong xp
!!DO85/21/41/1&y2<>3:P;

!?FU6003; uses v1-v10 base level points, v11 stack exp., x16=trial level, x1=factor, x2= stack exp. pts
!!VRy1:Svx16*x1;
!!FU&x2<y1:E; creature has less xp than this level's threshold - find the creature level in one of the later calls
; creature has enough xp to be level x16
; if x16=10, then creature is level 10
!!VRv11&x16=10:S10;
!!FU&x16=10:E;
; check if it has enough xp to be level x16+1
; if not, that means the creature is level x16
!!VRy2:Sx16+1;
!!VRy1:Svy2*x1;
!!VRv11&x2<y1:Sx16;
!!VRx16&x2<y1:S100; end loop

!?FU85; x16=stack, give enemy creatures stack experience
!!BMx16:T?y1 N?y2;
!!FU&y2<1:E;

!!VRy3:Sx16+1*-1;
;EAy3:E?y4/12/d/d; get level (N.G.)
!!EAy3:E?y8/2/d/d; note: E?y8/12/d/d doesn't work--can set but not get
!!VRv1:C20/40/64/92/124/160/200/244/294/350;
!!EAy1:L?y10; exp.-level-10 pts.
!!VRy9:Sy10:350; multiplier, =50*(MA:L?+1) except for level 8 cr and assuming the exp. pts. haven't been rescaled
; level "8" cr such as Rust Dragon or Lot have L10X=147,000=>level 8.4--still 147000/350=420 multiplier which works
; convert cr exp. pts (y8) to level (v11)
!!VRv11:S0;
!!DO6003/1/10/1:Py9/y8;
; now v11=stack experience level (0-10)
!!VRy8:S6-v68;
; increase enemy creature xp level based on difficulty,
; at 200% give creature levels 1/2/3/.../10 on EP 1/3/5/.../19, at 160 on 2/5/8/.../29, at 130% on 3/7/11/.../39, at 100% on 4/9/14/.../49, at 80% on 5/11/17/.../59
; decrease enemy creature xp level by 1 each week (starting fom week 2), due to Ice King's poor management
!!VRy5:Sv1477+1:y8+1-v2389;
!!VRy5&y5>10:S10;
!!FU&v11>=y5:E;
!!EAy3:Ey5/12/d/d;

!?BG0&1000;
!!BG:A?v1 N?v2;
!!FU&v1<>1:E;
!!IF:V200/1;
!!VRv1298:Sv2;

!?BG1&1000/200;
!!BG:N?v1;
!!FU&v1=v1298:E;
!!VRv1298:S-1;
!!BH0:N?v1;
!!BH1:N?v2;
!!BH0|v1=16/v1=18/v1=27:M0; Mephela(test only), Jenova, Gem
!!BH1|v2=18/v2=27:M0;
!!IF:V200/0;

!?BA1&1000;
!!BH0:N?v1; left hero
!!BH1:N?v2; right hero
!!FU&v1<>27/v2<>27:E; exit if not Leora (Gem)
!!HE27:O?v3;
!!FU&v3<>3:E; exit if Gem lost
!!DO138/0/8/1&v1=27:P;
!!DO138/21/29/1&v2=27:P;

!?BA53&1000;
!!BH0:N?v1; left hero
!!BH1:N?v2; right hero
!!FU&v1<>27/v2<>27/v1<>26/v2<>26:E; exit if not Leora (Gem) or Calladan (Elleshar)
!!HE27:O?v3;
!!FU&v3<>3:E; exit if Gem lost
!!HE26:O?v3;
!!FU&v3<>3:E; exit if Calladan lost
!!FU86:P1;

; update experience
!?FU86; x1=amount of exp to add, v1477=total exp
!!VRy3:Sv1477;
!!VRv1477:+x1;
!!VRy1:Sv1477+100000000;
!!HE27:Ey1; set Gem/Leora's exp to show mage/warrior exp
!!VRy2:Sv1477+10000000;
!!HE26:Ey2; set Elleshar/Calladan's exp to show mage/warrior exp
; promotions
!!if&y3<5/v1477=>5;
!!EA34:F76/-1/?v1; v1=line for ability 76/any
!!EA34&v1>-1:Bv1/1/76/24/30/30/30/30/30/40/40/40/40/40/50;  30/40/50% chance to deflect 25% of damage
!!IF:Q1/21/34/1^After the lessons of the experience are shared, the junior Mages see a way to improve their ability to deflect blows!^;
!!en;
!!if&y3<10/v1477=>10;
!!EA6:F76/-1/?v1; v1=line for ability 76/any
!!EA6&v1>-1:Bv1/1/76/24/30/30/30/30/30/40/40/40/40/40/50;  30/40/50% chance to deflect 25% of damage
!!IF:Q1/21/6/1^After the lessons of the experience are shared, the junior Warriors see a way to improve their ability to deflect blows!^;
!!en;
!!if&y3<15/v1477=>15;
!!EA34:F76/-1/?v1; v1=line for ability 76/any
!!EA34&v1>-1:Bv1/1/76/24/35/35/35/35/35/45/45/45/45/45/55;  35/45/55% chance to deflect 30% of damage
!!IF:Q1/21/34/1^After the lessons of the experience are shared, the junior Mages have gained more ability to deflect blows!^;
!!en;
!!if&y3<20/v1477=>20;
!!EA6:F76/-1/?v1; v1=line for ability 76/any
!!EA6&v1>-1:Bv1/1/76/24/35/35/35/35/35/45/45/45/45/45/55;  35/45/55% chance to deflect 30% of damage
!!IF:Q1/21/6/1^After the lessons of the experience are shared, the junior Warriors see a better way to deflect blows!^;
!!en;
!!if&y3<25/v1477=>25;
!!DO87/0/6/1:P26/34/35; chk Calladan
!!DO87/0/6/1:P27/34/35; chk Leora
!!DO94/0/6/1:P72/72/1/34/35; chk Woodshire
!!CA72/72/1:B6/41; build upgraded level 5
!!IF:Q1/21/34/21/35/1^After the lessons of the experience are shared, the junior Mages have gained enough wisdom to become High Mages!^;
!!UN:R1;
!!en;
!!if&y3<30/v1477=>30;
!!DO87/0/6/1:P26/6/7; chk Calladan
!!DO87/0/6/1:P27/6/7; chk Leora
!!DO94/0/6/1:P72/72/1/6/7; chk Woodshire
!!CA72/72/1:B6/40; build upgraded level 5
!!IF:Q1/21/6/21/7/1^After the lessons of the experience are shared, the junior Warriors have gained enough battle knowledge to become Senior Warriors!^;
!!UN:R1;
!!en;
!!if&y3<35/v1477=>35;
!!EA35:F112/-1/?v1; v1=line for ability 112/(any)
!!EA35&v1>-1:Bv1/1/112/19/100/100/100/100/100/100/100/100/100/100/100; Chain Lightning before attack
!!IF:Q1/21/35/1^After the lessons of the experience are shared, the High Mages have gained enough wisdom to learn Chain Lightning!^;
!!en;
!!if&y3<40/v1477=>40;
!!EA7:F101/-1/?v1; v1=line for ability 101/(any)
!!EA7&v1>-1:Bv1/1/101/61/5/7/10/12/15/17/20/22/25/27/30; Death Blow 5-30% (each blow)
!!EA174:F101/-1/?v1; v1=line for ability 101/(any)
!!EA174&v1>-1:Bv1/1/101/61/5/8/10/13/15/18/20/23/25/28/30; Death Blow 5-30% (each blow)
!!IF:Q1/21/7/1^After the lessons of the experience are shared, the Senior Warriors see a way to improve their ability to deal Death Blows!^;
!!en;
!!if&y3<45/v1477=>45;
!!EA35:F76/-1/?v1; v1=line for ability 76/any
!!EA35&v1>-1:Bv1/1/76/39/45/45/45/45/45/55/55/55/55/55/65;  45/55/65% chance to deflect 40% of damage
!!IF:Q1/21/35/1^After the lessons of the experience are shared, the High Mages see a way to improve their ability to deflect blows!^;
!!en;
!!if&y3<50/v1477=>50;
!!EA7:F76/-1/?v1; v1=line for ability 76/any
!!EA7&v1>-1:Bv1/1/76/39/45/45/45/45/45/55/55/55/55/55/65;  45/55/65% chance to deflect 40% of damage
!!IF:Q1/21/7/1^After the lessons of the experience are shared, the Senior Warriors see a way to improve their ability to deflect blows!^;
!!en;
!!if&y3<55/v1477=>55;
!!EA35:F112/-1/?v1; v1=line for ability 112/(any)
!!EA35&v1>-1:Bv1/1/112/57/100/100/100/100/100/100/100/100/100/100/100; TTB before attack
!!IF:Q1/21/35/1^After the lessons of the experience are shared, the High Mages have gained enough wisdom to learn the Titan's Thunderbolt!^;
!!en;
!!if&y3<60/v1477=>60;
!!EA7:F101/-1/?v1; v1=line for ability 101/(any)
!!EA7&v1>-1:Bv1/1/101/61/10/12/15/17/20/22/25/27/30/32/35; Death Blow 10-35% (each blow)
!!EA174:F101/-1/?v1; v1=line for ability 101/(any)
!!EA174&v1>-1:Bv1/1/101/61/10/13/15/18/20/23/25/28/30/33/35; Death Blow 10-35% (each blow)
!!EA174:F115/-1/?v1; v1=line for ability 115/-1, or empty line if none, -1 if no free lines
!!EA174&v1>-1:Bv1/1/115/53/0/0/1/1/1/2/2/2/3/3/3; Enchant w/Haste (3=expert--match Jalery)
!!IF:Q1/21/7/1^After the lessons of the experience are shared, the Senior Warriors see a way to improve their ability to deal Death Blows!^;
!!en;
!!if&y3<65/v1477=>65;
!!EA35:F76/-1/?v1; v1=line for ability 76/any
!!EA35&v1>-1:Bv1/1/76/44/50/50/50/50/50/60/60/60/60/60/70;  50/60/70% chance to deflect 45% of damage
!!IF:Q1/21/35/1^After the lessons of the experience are shared, the High Mages see a way to improve their ability to deflect blows!^;
!!en;
!!if&y3<70/v1477=>70;
!!EA7:F76/-1/?v1; v1=line for ability 76/any
!!EA7&v1>-1:Bv1/1/76/44/50/50/50/50/50/60/60/60/60/60/70;  50/60/70% chance to deflect 45% of damage
!!IF:Q1/21/7/1^After the lessons of the experience are shared, the Senior Warriors see a way to improve their ability to deflect blows!^;
!!en;
!!if&y3<75/v1477=>75;
!!EA35:F76/-1/?v1; v1=line for ability 76/any
!!EA35&v1>-1:Bv1/1/76/49/55/55/55/55/55/65/65/65/65/65/75;  55/65/75% chance to deflect 50% of damage
!!IF:Q1/21/35/1^After the lessons of the experience are shared, the High Mages see a way to improve further their ability to deflect blows!^;
!!en;
!!if&y3<80/v1477=>80;
!!EA7:F76/-1/?v1; v1=line for ability 76/any
!!EA7&v1>-1:Bv1/1/76/49/55/55/55/55/55/65/65/65/65/65/75;  55/65/75% chance to deflect 50% of damage
!!IF:Q1/21/7/1^After the lessons of the experience are shared, the Senior Warriors see a way to improve further  their ability to deflect blows!^;
!!en;
!!if&y3<85/v1477=>85;
!!EA174:B12/1/103/37/25/25/25/25/25/35/35/35/35/35/45;  resist 20/30/40% of spell damage
!!IF:Q1/21/174/1^After much practice, Vames has improved his resistance to spell damage!^;
!!en;
!!if&y3<90/v1477=>90;
!!EA174:B12/1/103/37/30/30/30/30/30/40/40/40/40/40/50;  resist 20/30/40% of spell damage
!!IF:Q1/21/174/1^Vames has strengthened his resistance to spell damage further!^;
!!en;
!!if&y3<95/v1477=>95;
!!HE27:M48/1;
!!IF:Q1/21/128/9/48/1^For all her efforts, the Elder Gods have awarded Leora a new power!^;
!!en;
!!if&y3<100/v1477=>100;
!!HE27:M38/1;
!!IF:Q1/21/128/9/38/1^In admiration of her dedication, the Elder Gods have awarded Leora another new power!^;
!!en;
!!if&y3<110/v1477=>110;
!!HE27:M9/1;
!!IF:Q1/21/128/9/9/1^In return for her faithful service, the Elder Gods have given Leora another new power!^;
!!en;
!!if&y3<120/v1477=>120;
!!HE27:S18/3;
!!IF:Q1/21/128/20/59/1^In recognition of her thorough mastery of Life spells, the Elemental Acadamies have awarded Leora the credential of Expert Scholar!^;
!!en;
!!if&y3<130/v1477=>130;
!!HE27:S24/3;
!!IF:Q1/21/128/20/77/1^For outstanding achievement in the field of excellence, the Elemental Acadamies have awarded Leora the certificate of Expert Intelligence!^;
!!en;
!!if&y3<140/v1477=>140;
!!HE27:S25/3;
!!IF:Q1/21/128/20/80/1^In recognition of the speed and skill with which she casts spells, the Elemental Acadamies have awarded Leora the credential of Expert Sorcery!^;
!!en;
!!if&y3<150/v1477=>150;
!!HE27:A4/72;
!!IF:Q1/21/128/8/72/1^The Elder Gods have rewarded Leora with their ultimate prize: the Cloak of Levitation!^;
!!en;

!?FU87; x16=troop, x1=HE#, x2=cr#, x3=new cr#
!!HEx1:C0/x16/?y1/d;
!!HEx1&y1=x2:C0/x16/x3/d;

!?FU94; x16=troop. x1/x2/x3=Town loc., x4=old cr#, x5=new cr#
!!CAx1/x2/x3:M2/x16/?y1/d;
!!CAx1/x2/x3&y1=x4:M2/x16/x5/d;

!?FU138; x16=stack
!!BMx16:T?y1 N?y2 O?y3;
!!FU&y1<>174/y1<>136|y2>0/y3<0:E;
!!VRy4:Sx16+1*-1;
!!EAy4:E?y5/2/d/d;
!!HE27:C0/y3/y1/1/y5/2;
!!IF&y1=174:Q1/21/128/1^{Leora}:  Vames was badly wounded, but I have healed him!^;
!!IF&y1=136:Q1/21/128/1^{Leora}:  Jalery was badly wounded, but I have healed him!^;
!!UN:R1;

!?HL-1&v2392>1; Hero Level Up (at Tree of K)
!!HE-1:O?y1;
!!FU&y1<>3:E; green only
!!VRy2:S0 R2;
!!HL:Sy2/-1/-1; no SS chg

!$OB102/0; post-Tree
!!FU86:P1;
